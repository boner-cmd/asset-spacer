// a tool to space out bulk-imported assets across a grid

// from help.autodesk.com sample MEL code
// https://help.autodesk.com/view/MAYAUL/2020/ENU/index.html?contextId=COMMANDS-INDEX

// and publicly-accessible content from Professor Alan Price's Autumn 2005
// "Experimental Scripting for Animation in Maya" course at Ohio State University
// https://www.asc.ohio-state.edu/price.566/courses/694/

global proc aSpacer()
{
  if (`window -exists aspWindow`) deleteUI aspWindow;
  float $spaceInput;
  float $matrixRatio;
  window
    -title "Asset Spacer"
    -iconName "aspWindow"
    -maximizeButton false
    -resizeToFitChildren true
    -sizeable false
    "aspWindow";
  columnLayout
    -columnAttach "left" 5
    -rowSpacing 10;

  // need to add a text object as a label
  text
    -label "Matrix Object Spacing"
    "spacingLabel";
  floatField
    -minValue 0
    -value 0
    -precision 2
    -changeCommand "$spaceInput = `floatField -query -value spacingField`"
    -enterCommand "setFocus startButton"
    "spacingField";
  radioButtonGrp
    -enableKeyboardFocus true
    -numberOfRadioButtons 4
    -label "Matrix dimensions (approximate)"
    -labelArray4 "1x1" "4x3" "3x2" "2x1"
    -annotation1 "Ratio of 1.00"
    -annotation2 "Ratio of 1.33"
    -annotation3 "Ratio of 1.50"
    -annotation4 "Ratio of 2.00"
    -onCommand1 "$matrixRatio = 1.00"
    -onCommand2 "$matrixRatio = 1.33"
    -onCommand3 "$matrixRatio = 1.50"
    -onCommand4 "$matrixRatio = 2.00"
    "matrixRadioBtn";
  button
    -enableKeyboardFocus true
    -label "Go"
    -command "spaceObjs($spaceInput,$matrixRatio)"
    "startButton";
  showWindow aspWindow;
}

proc spaceObjs( float $spacing,float $ratio) {
  string $selection[];
  $selection = `ls -sl`;
  $selectionSize = size($selection);
  if ( $selectionSize > 1 ) {
    print "More than one object selected.";
  } else if ( $selectionSize == 0 ) {
    print "No object selected.";
  } else if ( $selectionSize < 0 ) {
    select -clear;
    print "Negative selection detected.";
  } else {
    string $givenObjects[];
    $givenObjects = `listRelatives $selection`;
    $shapeCheck = `listRelatives -s $selection`;
    if ( size($shapeCheck) != 0 ) {
      print "Selection has Shape children. Are you sure a group is selected?";
    } else {
      int $xcount = 0;
      int $zcount = 0;
      $squareish = int(sqrt(size($givenObjects)) * $ratio);
      for ( $obj in $givenObjects ) {
        string $transobj_x = $obj + ".translateX";
        string $transobj_z = $obj + ".translateZ";
        string $transobj_y = $obj + ".translateY";
        if ( $xcount == $squareish ) {
          $xcount = 0;
          $zcount += 1;
        }
        setAttr $transobj_y 0;
        setAttr $transobj_x ($xcount * $spacing);
        setAttr $transobj_z ($zcount * $spacing);
        $xcount += 1;
      }
    }
  }
}
